//Parallel Adder/Subtractor using 16-bit Ripple Carry Adder
CHIP RCA16b
{
   IN x[16],y[16],AorS; 
   //Takes input as a 16-bit signed integer X,a 16-bit signed integer Y and bit AorS to decide Addition or Subtraction
   //AorS=0 implies Addition
   //AorS=1 implies Subtraction
   OUT z[16],OVF;
   //Outputs a 16-bit signed integer Z and Overflow bit
   PARTS:
   //Sequences of XOR gates changes Y into 2's Complement if AorS is subtraction
   //Y remains unchanged if AorS is addition
   Xor(a=AorS, b=y[0], out=y0);
   Xor(a=AorS, b=y[1], out=y1);
   Xor(a=AorS, b=y[2], out=y2);
   Xor(a=AorS, b=y[3], out=y3);
   Xor(a=AorS, b=y[4], out=y4);
   Xor(a=AorS, b=y[5], out=y5);
   Xor(a=AorS, b=y[6], out=y6);
   Xor(a=AorS, b=y[7], out=y7);
   Xor(a=AorS, b=y[8], out=y8);
   Xor(a=AorS, b=y[9], out=y9);
   Xor(a=AorS, b=y[10], out=y10);
   Xor(a=AorS, b=y[11], out=y11);
   Xor(a=AorS, b=y[12], out=y12);
   Xor(a=AorS, b=y[13], out=y13);
   Xor(a=AorS, b=y[14], out=y14);
   Xor(a=AorS, b=y[15], out=y15);
   //Using Fulladder gate to perform addition between carry bit and both the inputs of X,Y
   Fulladder(xi=x[0], yi=y0, ci=AorS, si=z[0], ci1=c1);
   Fulladder(xi=x[1], yi=y1, ci=c1, si=z[1], ci1=c2);
   Fulladder(xi=x[2], yi=y2, ci=c2, si=z[2], ci1=c3);
   Fulladder(xi=x[3], yi=y3, ci=c3, si=z[3], ci1=c4);
   Fulladder(xi=x[4], yi=y4, ci=c4, si=z[4], ci1=c5);
   Fulladder(xi=x[5], yi=y5, ci=c5, si=z[5], ci1=c6);
   Fulladder(xi=x[6], yi=y6, ci=c6, si=z[6], ci1=c7);
   Fulladder(xi=x[7], yi=y7, ci=c7, si=z[7], ci1=c8);
   Fulladder(xi=x[8], yi=y8, ci=c8, si=z[8], ci1=c9);
   Fulladder(xi=x[9], yi=y9, ci=c9, si=z[9], ci1=c10);
   Fulladder(xi=x[10], yi=y10, ci=c10, si=z[10], ci1=c11);
   Fulladder(xi=x[11], yi=y11, ci=c11, si=z[11], ci1=c12);
   Fulladder(xi=x[12], yi=y12, ci=c12, si=z[12], ci1=c13);
   Fulladder(xi=x[13], yi=y13, ci=c13, si=z[13], ci1=c14);
   Fulladder(xi=x[14], yi=y14, ci=c14, si=z[14], ci1=c15);   
   Fulladder(xi=x[15], yi=y15, ci=c15, si=z[15], ci1=c16);
   //Performs XOR operation between carry15 and carry16 to find the overflow bit
   Xor(a=c15, b=c16, out=OVF);
}
	